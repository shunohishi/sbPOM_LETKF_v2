program main

  use setting
  use mod_julian
  use mod_stat
  use mod_read_tide
  use mod_io
  use mod_rmiss
  implicit none

  !---Common
  integer iyr,imon,iday
  integer syr,smon,sday
  integer eyr,emon,eday
  integer ijul,sjul,ejul
  integer idat_a,jdat_a
  integer ist
  integer ist_ave(ndat_a),ist_min(ndat_a),ist_max(ndat_a)

  real(kind = 8),allocatable :: dist(:,:,:) !nst,njul
  real(kind = 8) dist_ave(nst,ndat_a)

  real(kind = 8) tmp
  
  !---Analysis in observation space
  real(kind = 8),allocatable :: lon_a(:,:,:),lat_a(:,:,:)
  real(kind = 8),allocatable :: hmean_a(:,:,:),hsprd_a(:,:,:)

  real(kind = 8) lon_a_ave(nst,ndat_a),lat_a_ave(nst,ndat_a)
  real(kind = 8) hmean_a_ave(nst,ndat_a),hsprd_a_ave(nst,ndat_a)
  
  !---Observation
  real(kind = 8),allocatable :: lon_o(:,:),lat_o(:,:)
  real(kind = 8),allocatable :: dat_o(:,:)

  real(kind = 8) lon_o_ave(nst),lat_o_ave(nst)
  real(kind = 8) dat_o_ave(nst)
  
  !---Statistics
  !Monthly
  integer,allocatable :: num_mave(:,:,:,:)
  
  real(kind = 8),allocatable :: bias_mave(:,:,:,:) !nst,ndata,nmon,nyr
  real(kind = 8),allocatable :: abias_mave(:,:,:,:)
  real(kind = 8),allocatable :: rmsd_mave(:,:,:,:)
  real(kind = 8),allocatable :: sprd_mave(:,:,:,:)

  real(kind = 8),allocatable :: abias_mave_all(:,:,:) !ndata,nmon,nyr
  real(kind = 8),allocatable :: rmsd_mave_all(:,:,:)
  real(kind = 8),allocatable :: sprd_mave_all(:,:,:)
  
  !All period
  integer num_ave(nst,ndat_a)

  real(kind = 8) bias_ave(nst,ndat_a)
  real(kind = 8) rmsd_ave(nst,ndat_a)
  real(kind = 8) sprd_ave(nst,ndat_a)
  
  !All station
  real(kind = 8) bias_ave_all(ndat_a)
  real(kind = 8) rmsd_ave_all(ndat_a)
  real(kind = 8) sprd_ave_all(ndat_a)

  !Bootstrap
  real(kind = 8) abias_dif_low(nst,ndat_a,ndat_a),abias_dif_ave(nst,ndat_a,ndat_a),abias_dif_upp(nst,ndat_a,ndat_a)
  real(kind = 8) rmsd_dif_low(nst,ndat_a,ndat_a),rmsd_dif_ave(nst,ndat_a,ndat_a),rmsd_dif_upp(nst,ndat_a,ndat_a)

  real(kind = 8) abias_dif_low_all(ndat_a,ndat_a),abias_dif_ave_all(ndat_a,ndat_a),abias_dif_upp_all(ndat_a,ndat_a)
  real(kind = 8) rmsd_dif_low_all(ndat_a,ndat_a),rmsd_dif_ave_all(ndat_a,ndat_a),rmsd_dif_upp_all(ndat_a,ndat_a)
  
  write(*,*) "### START: Validation vs. tide gauge"
  
  !---Read start and end dates
  call read_argument(syr,smon,sday,eyr,emon,eday)

  write(*,*) "Start date(validation):",syr,smon,sday
  write(*,*) "End date(validation):",eyr,emon,eday  

  !---Julian day
  call ymd_julian(syr,smon,sday,sjul)
  call ymd_julian(eyr,emon,eday,ejul)

  !---Allocate
  allocate(dist(nst,sjul:ejul,ndat_a))

  allocate(lon_a(nst,sjul:ejul,ndat_a),lat_a(nst,sjul:ejul,ndat_a))
  allocate(hmean_a(nst,sjul:ejul,ndat_a),hsprd_a(nst,sjul:ejul,ndat_a))

  allocate(lon_o(nst,sjul:ejul),lat_o(nst,sjul:ejul))
  allocate(dat_o(nst,sjul:ejul))

  !===Read data===============================================================
  do idat_a=1,ndat_a

     !---Read data     
     do ijul=sjul,ejul

        call julian_ymd(ijul,iyr,imon,iday)
        write(*,*) idat_a,iyr,imon,iday

        call read_hdat(idat_a,nst,ijul, &
             & lon_a(:,ijul,idat_a),lat_a(:,ijul,idat_a),hmean_a(:,ijul,idat_a),hsprd_a(:,ijul,idat_a), &
             & lon_o(:,ijul),lat_o(:,ijul),dat_o(:,ijul),dist(:,ijul,idat_a))
        
     end do !ijul

     !---Remove the tide gage station "Ofunato" from Validation
     !   due to Great East Japan Earthquake
     ist=351
     dat_o(ist,sjul:ejul)=rmiss
     
     !---Average
     do ist=1,nst
        !Analysis
        call average_rmiss(ejul-sjul+1,lon_a(ist,sjul:ejul,idat_a),lon_a_ave(ist,idat_a))
        call average_rmiss(ejul-sjul+1,lat_a(ist,sjul:ejul,idat_a),lat_a_ave(ist,idat_a))
        call average_rmiss(ejul-sjul+1,hmean_a(ist,sjul:ejul,idat_a),hmean_a_ave(ist,idat_a))
        call average_rmiss(ejul-sjul+1,hsprd_a(ist,sjul:ejul,idat_a),hsprd_a_ave(ist,idat_a))
        !Obs
        call average_rmiss(ejul-sjul+1,lon_o(ist,sjul:ejul),lon_o_ave(ist))
        call average_rmiss(ejul-sjul+1,lat_o(ist,sjul:ejul),lat_o_ave(ist))
        call average_rmiss(ejul-sjul+1,dat_o(ist,sjul:ejul),dat_o_ave(ist))
        !Distance
        call average_rmiss(ejul-sjul+1,dist(ist,sjul:ejul,idat_a),dist_ave(ist,idat_a))        
     end do !ist

     !---Remove reference level
     do ist=1,nst
        call remove_reference_level(ejul-sjul+1,hmean_a(ist,sjul:ejul,idat_a),hmean_a_ave(ist,idat_a))
        call remove_reference_level(ejul-sjul+1,dat_o(ist,sjul:ejul),dat_o_ave(ist))
     end do !ist
     
  end do !idat_a

  !---Write data
  call write_ts(ndat_a,nst,sjul,ejul,hmean_a,dat_o)
     
  !===Statistics==============================================================
  !---Allocate
  allocate(num_mave(nst,ndat_a,12,syr:eyr))
  allocate(bias_mave(nst,ndat_a,12,syr:eyr))
  allocate(abias_mave(nst,ndat_a,12,syr:eyr))
  allocate(rmsd_mave(nst,ndat_a,12,syr:eyr))
  allocate(sprd_mave(nst,ndat_a,12,syr:eyr))

  allocate(abias_mave_all(ndat_a,12,syr:eyr))
  allocate(rmsd_mave_all(ndat_a,12,syr:eyr))
  allocate(sprd_mave_all(ndat_a,12,syr:eyr))
  
  !---Initialize
  num_mave(:,:,:,:)=0
  bias_mave(:,:,:,:)=0.d0
  abias_mave(:,:,:,:)=0.d0
  rmsd_mave(:,:,:,:)=0.d0
  sprd_mave(:,:,:,:)=0.d0

  num_ave(:,:)=0
  bias_ave(:,:)=0.d0
  rmsd_ave(:,:)=0.d0
  sprd_ave(:,:)=0.d0

  !---Add
  do idat_a=1,ndat_a
     do ijul=sjul,ejul

        call julian_ymd(ijul,iyr,imon,iday)

        !---Monthly
        call stat_1d_add(nst,hmean_a(:,ijul,idat_a),hsprd_a(:,ijul,idat_a),dat_o(:,ijul), &
             & num_mave(:,idat_a,imon,iyr),bias_mave(:,idat_a,imon,iyr), &
             & rmsd_mave(:,idat_a,imon,iyr),sprd_mave(:,idat_a,imon,iyr))

        !---ALL
        call stat_1d_add(nst,hmean_a(:,ijul,idat_a),hsprd_a(:,ijul,idat_a),dat_o(:,ijul), &
             & num_ave(:,idat_a),bias_ave(:,idat_a), &
             & rmsd_ave(:,idat_a),sprd_ave(:,idat_a))

     end do    !ijul
  end do       !idat_a

  !---Finalize
  do idat_a=1,ndat_a

     !Monthly
     do imon=1,12
        do iyr=syr,eyr
           call stat_1d_end(nst,num_mave(:,idat_a,imon,iyr), &
                & bias_mave(:,idat_a,imon,iyr),rmsd_mave(:,idat_a,imon,iyr),sprd_mave(:,idat_a,imon,iyr))
           
        end do !iyr
     end do    !imon

     !ALL
     call stat_1d_end(nst,num_ave(:,idat_a), &
          & bias_ave(:,idat_a), &
          & rmsd_ave(:,idat_a), &
          & sprd_ave(:,idat_a))
        
  end do !idat_a

  !Absolute bias
  do ist=1,nst
     do idat_a=1,ndat_a
        call convert_absolute_bias((eyr-syr+1)*12,bias_mave(ist,idat_a,:,syr:eyr),abias_mave(ist,idat_a,:,syr:eyr))
     end do
  end do
  
  !Average for all station
  do idat_a=1,ndat_a
     do imon=1,12
        do iyr=syr,eyr
           call average_rmiss(nst,abias_mave(:,idat_a,imon,iyr),abias_mave_all(idat_a,imon,iyr))      
           call average_rmiss(nst,rmsd_mave(:,idat_a,imon,iyr),rmsd_mave_all(idat_a,imon,iyr))      
           call average_rmiss(nst,sprd_mave(:,idat_a,imon,iyr),sprd_mave_all(idat_a,imon,iyr))      
        end do
     end do

     call average_rmiss(nst,bias_ave(:,idat_a),bias_ave_all(idat_a))
     call average_rmiss(nst,rmsd_ave(:,idat_a),rmsd_ave_all(idat_a))
     call average_rmiss(nst,sprd_ave(:,idat_a),sprd_ave_all(idat_a))
     
  end do
  
  !---Bootstrap
  do ist=1,nst
     
     !Initialize
     abias_dif_low(ist,:,:)=rmiss
     abias_dif_ave(ist,:,:)=rmiss
     abias_dif_upp(ist,:,:)=rmiss
     rmsd_dif_low(ist,:,:)=rmiss
     rmsd_dif_ave(ist,:,:)=rmiss
     rmsd_dif_upp(ist,:,:)=rmiss

     !Paired t-test
     do idat_a=1,ndat_a
        do jdat_a=1,ndat_a

           if(idat_a == jdat_a) cycle

           !Absolute bias
           call bootstrap((eyr-syr+1)*12,abias_mave(ist,idat_a,:,syr:eyr),abias_mave(ist,jdat_a,:,syr:eyr), &
                & abias_dif_low(ist,idat_a,jdat_a),abias_dif_ave(ist,idat_a,jdat_a),abias_dif_upp(ist,idat_a,jdat_a))

           !RMSD
           call bootstrap((eyr-syr+1)*12,rmsd_mave(ist,idat_a,:,syr:eyr),rmsd_mave(ist,jdat_a,:,syr:eyr), &
                & rmsd_dif_low(ist,idat_a,jdat_a),rmsd_dif_ave(ist,idat_a,jdat_a),rmsd_dif_upp(ist,idat_a,jdat_a))
           
        end do !jdat_a
     end do    !idat_a
     
  end do !ist

  !Initialize
  abias_dif_low_all(:,:)=rmiss
  abias_dif_ave_all(:,:)=rmiss
  abias_dif_upp_all(:,:)=rmiss
  rmsd_dif_low_all(:,:)=rmiss
  rmsd_dif_ave_all(:,:)=rmiss
  rmsd_dif_upp_all(:,:)=rmiss
  
  do idat_a=1,ndat_a
     do jdat_a=1,ndat_a

        if(idat_a == jdat_a) cycle

        !Absolute bias
        call bootstrap((eyr-syr+1)*12,abias_mave_all(idat_a,:,syr:eyr),abias_mave_all(jdat_a,:,syr:eyr), &
             & abias_dif_low_all(idat_a,jdat_a),abias_dif_ave_all(idat_a,jdat_a),abias_dif_upp_all(idat_a,jdat_a))

        call bootstrap((eyr-syr+1)*12,rmsd_mave_all(idat_a,:,syr:eyr),rmsd_mave_all(jdat_a,:,syr:eyr), &
             & rmsd_dif_low_all(idat_a,jdat_a),rmsd_dif_ave_all(idat_a,jdat_a),rmsd_dif_upp_all(idat_a,jdat_a))
                
     end do
  end do

  !---Write data
  call write_stat_mave(nst,ndat_a,syr,eyr,lon_o_ave,lat_o_ave, &
       & num_mave,bias_mave,rmsd_mave,sprd_mave)
  call write_stat_ave(nst,ndat_a,lon_o_ave,lat_o_ave, &
       & num_ave,bias_ave,rmsd_ave,sprd_ave, &
       & abias_dif_low,abias_dif_ave,abias_dif_upp, &
       & rmsd_dif_low,rmsd_dif_ave,rmsd_dif_upp)
  call write_stat_ave_all(ndat_a,bias_ave_all,rmsd_ave_all,sprd_ave_all, &
       & abias_dif_low_all,abias_dif_ave_all,abias_dif_upp_all, &
       & rmsd_dif_low_all,rmsd_dif_ave_all,rmsd_dif_upp_all)
       
  !===Post process======================================================================
  !---Station closest to averaged RMSD
  ist_ave(:)=0
  do idat_a=1,ndat_a
     call station_close_to_ave(nst,rmsd_ave(:,idat_a),rmsd_ave_all(idat_a),ist_ave(idat_a))     
  end do

  !---Station with best/worst RMSD ratio (vs. Reference dataset=First dataset)
  ist_min(:)=0
  ist_max(:)=0
  do idat_a=2,ndat_a
     call station_best_worst(nst,rmsd_ave(:,1),rmsd_ave(:,idat_a),ist_min(idat_a),ist_max(idat_a))
  end do

  !---Write data
  !Ave statio
  do idat_a=1,ndat_a
     write(*,'(i6,a,i6)') idat_a,"Station (Ave):",ist_ave(idat_a)     
  end do

  !Min/Max station (Worst/Best)
  do idat_a=2,ndat_a
     write(*,'(i6,a,i6,a,i6)') idat_a,"Station (Worst):",ist_min(idat_a),"Station (Best):",ist_max(idat_a)
  end do
  
  !=== END =========================================================================
  
  !---Deallocate
  deallocate(dist)

  deallocate(lon_a,lat_a)
  deallocate(hmean_a,hsprd_a)

  deallocate(lon_o,lat_o)
  deallocate(dat_o)

  deallocate(num_mave)
  deallocate(bias_mave)
  deallocate(abias_mave)
  deallocate(rmsd_mave)
  deallocate(sprd_mave)

  deallocate(abias_mave_all)
  deallocate(rmsd_mave_all)
  deallocate(sprd_mave_all)
  
  write(*,*) "### END: Validation vs. tide gauge"

  
end program main

!----------------------------------------------------------------

subroutine remove_reference_level(n,dat,ave)

  use mod_rmiss
  implicit none

  !---Common
  integer i
  
  !---IN
  integer,intent(in) :: n
  real(kind = 8),intent(in) :: ave

  !---OUT
  real(kind = 8),intent(out) :: dat(n)

  if(ave == rmiss)then
     return
  end if
  
  do i=1,n
     if(dat(i) == rmiss) cycle
     dat(i)=dat(i)-ave     
  end do
  
end subroutine remove_reference_level

!----------------------------------------------------------------

subroutine station_close_to_ave(n,dat,ave,iout)

  use mod_rmiss
  implicit none

  !---Common
  integer i

  real(kind = 8) adif(n)
  real(kind = 8) adif_min
  
  !---IN
  integer,intent(in) :: n

  real(kind = 8),intent(in) :: dat(n)
  real(kind = 8),intent(in) :: ave

  !---OUT
  integer,intent(out) :: iout
  
  do i=1,n
     if(dat(i) == rmiss)then
        adif(i)=rmiss
     else
        adif(i)=abs(dat(i)-ave)
     end if
  end do

  adif_min=minval(adif, mask=adif /= rmiss)
           
  do i=1,n
     if(adif(i) == adif_min)then
        iout=i
        exit
     end if
  end do
  
end subroutine station_close_to_ave

!---------------------------------------------------------------------

subroutine station_best_worst(n,rmsd_ref,rmsd,imin,imax)

  use mod_rmiss
  implicit none

  !---Common
  integer i

  real(kind = 8) ratio(n)
  real(kind = 8) ratio_min,ratio_max

  !---IN
  integer,intent(in) :: n

  real(kind = 8) rmsd_ref(n),rmsd(n)

  !---OUT
  integer,intent(out) :: imin,imax

  do i=1,n
     if(rmsd_ref(i) == rmiss .or. rmsd(i) == rmiss)then
        ratio(i)=rmiss
     else
        ratio(i)=100.d0*(rmsd(i)-rmsd_ref(i))/rmsd_ref(i)
     end if     
  end do

  ratio_min=minval(ratio, mask=ratio /= rmiss)
  ratio_max=maxval(ratio, mask=ratio /= rmiss)

  do i=1,n
     if(ratio(i) == ratio_min)then
        imin=i
        exit
     end if
  end do

  do i=1,n
     if(ratio(i) == ratio_max)then
        imax=i
        exit
     end if
  end do
    
end subroutine station_best_worst
